repositories {
    repositories.mavenCentral()
    maven {
        // To be able to use a patched version of sevenzipjbinding-all-platforms
        // From the following repo: https://gitlab.com/shayartzi/sevenzipjbinding/-/packages/27273903
        url = uri("https://gitlab.com/api/v4/projects/59936705/packages/maven")
    }
}

dependencies {
    api project(':mucommander-commons-file')

    comprise 'net.sf.sevenzipjbinding:sevenzipjbinding:16.02-2.01'

    // Use a patched version of sevenzipjbinding-all-platforms for Mac M1 support.
    // See https://github.com/mucommander/mucommander/pull/1237
    comprise 'com.github.shayartzi.sevenzipjbinding:sevenzipjbinding-all-platforms:16.02-2.01'

    testImplementation 'org.testng:testng:7.10.2'
}

jar {
   from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it).matching {
        exclude 'META-INF/**'
   } }
   bnd ('Bundle-Name': 'muCommander-sevenzipjbindings',
        'Bundle-Vendor': 'muCommander',
        'Bundle-Description': 'Wrapper for SevenZipJBindings',
        'Bundle-DocURL': 'https://www.mucommander.com',
        'Export-Package': 'com.mucommander.sevenzipjbindings,net.sf.sevenzipjbinding',
        'Import-Package': '*',
        'Specification-Title': "muCommander",
        'Specification-Vendor': "Arik Hadas",
        'Specification-Version': project.version,
        'Implementation-Title': "muCommander",
        'Implementation-Vendor': "Arik Hadas",
        'Implementation-Version': revision.substring(0, 7),
        'Build-Date': new Date().format('yyyyMMdd'),
        'Build-Url': "https://www.mucommander.com/version/nightly.xml")
}

